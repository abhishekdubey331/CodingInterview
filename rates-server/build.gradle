group 'RevolutTestExchangeRates'
version '0.1-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.3.60'
    ext.ktor_version = '1.2.6'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlinx:atomicfu-gradle-plugin:0.13.1"
        classpath "org.jetbrains.kotlinx:kotlinx-metadata-jvm:0.0.5.1"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'kotlinx-atomicfu'

mainClassName = 'ServerKt'

sourceCompatibility = 1.8

sourceSets {
    main {
        resources {
            srcDirs "src/main/resources"
        }
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'ServerKt'
    }

    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

repositories {
    mavenCentral()
    jcenter() {
        content {
            excludeGroupByRegex "com\\.revolut.*"
        }
    }
    maven {
        name "Revolut snapshots repository"
        url "gcs://repo.revolut.com/snapshots"
        content {
            includeGroupByRegex "com\\.revolut.*"
        }
        mavenContent {
            snapshotsOnly()
        }
    }
    maven {
        name "Revolut releases repository"
        url "gcs://repo.revolut.com/releases"
        content {
            includeGroupByRegex "com\\.revolut.*"
        }
        mavenContent {
            releasesOnly()
        }
    }
}

dependencies {
    compile "com.revolut.common:common-infra:1.0-SNAPSHOT"
    compile "ch.qos.logback:logback-classic:1.2.3"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "io.ktor:ktor-server-netty:$ktor_version"
    compile "io.ktor:ktor-gson:$ktor_version"
    testCompile group: 'junit', name: 'junit', version: '4.12'
}